plugins {
    id "com.android.library" version "3.4.1"
    id "org.jetbrains.kotlin.android" version "1.3.31"
    id "com.github.technoir42.aar-publish" version "1.0.2"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group = "com.github.technoir42"
version = "0.1"

ext.versions = [
    androidx          : [
        appcompat: "1.0.2",
        core     : "1.0.2",
        fragment : "1.1.0-beta01"
    ],

    kotlin            : "1.3.31",
    materialcomponents: "1.0.0",
]

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }

    lintOptions {
        disable "GradleDependency"
    }

    libraryVariants.all { variant ->
        variant.generateBuildConfigProvider.configure { enabled = false }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation "androidx.appcompat:appcompat:$versions.androidx.appcompat"
    implementation "androidx.core:core:$versions.androidx.core"
    implementation "androidx.core:core-ktx:$versions.androidx.core"
    implementation "androidx.fragment:fragment:$versions.androidx.fragment"
    implementation "com.google.android.material:material:$versions.materialcomponents"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.android
        }
    }
}

bintray {
    user = findProperty("BINTRAY_USER")
    key = findProperty("BINTRAY_KEY")
    publish = true

    pkg {
        repo = "maven"
        name = "android-extensions"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/technoir42/android-extensions.git"
        version {
            name = project.version
        }
        publications = ["maven"]
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
